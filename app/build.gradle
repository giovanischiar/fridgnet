plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    namespace 'io.schiar.fridgnet'
    compileSdk 33

    defaultConfig {
        applicationId "io.schiar.fridgnet"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "MAPS_API_KEY", apikeyProperties['MAPS_API_KEY']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.4"
    }

    afterEvaluate {
        packageDebugResources.dependsOn 'generateIcons'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation 'androidx.compose.material3:material3:1.2.0-alpha01'
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation 'androidx.compose.compiler:compiler:1.4.7'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Navigation
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // Google maps
    implementation 'com.google.maps.android:maps-compose:2.8.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'

    // Photo picker
    implementation 'io.coil-kt:coil-compose:2.2.0'

    // Get EXIF data (lat lng, date, etc) from pictures
    implementation 'androidx.exifinterface:exifinterface:1.3.6'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Gson
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Gson to convert raw JSON to pretty JSON
    implementation 'com.google.code.gson:gson:2.8.9'

    // Room for database persistence
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
}

task generateIcons(type: Exec) {
    dependsOn "mergeDebugResources"
    dependsOn "mapDebugSourceSetPaths"
    inputs.file 'src/main/res/values/iconpalette.xml'
    inputs.dir '../scripts/IconCreator/Icon'
    outputs.file 'src/main/res/drawable/ic_launcher_background.xml'
    outputs.file 'src/main/res/drawable-v24/ic_launcher_foreground.xml'
    commandLine 'bash', '../scripts/IconCreator/Library/icon-creator.sh', 'android', '../..', '../Icon'
}